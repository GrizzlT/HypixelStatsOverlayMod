
// For those who want the bleeding edge
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "2.0.2"
group= "com.github.grizzlt.hypixelstatsoverlay" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "HypixelStatsOverlayMod"

sourceCompatibility = targetCompatibility = 1.8

// Use UTF-8 for encoding
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations {
    shade
    compile.extendsFrom(shade)
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven/'
    }
    maven { url 'https://repo.hypixel.net/repository/Hypixel/' }
}

dependencies {
    shade('org.spongepowered:mixin:0.6.4-SNAPSHOT') {
        exclude module: 'launchwrapper'
    }

    shade 'com.github.GrizzlT:ServerBasedClientModLibrary:1.8.9-SNAPSHOT'
    compile 'com.github.GrizzlT:HypixelPublicAPIMod:library-SNAPSHOT'

    compile 'org.jetbrains:annotations:21.0.1'
}

mixin {
    add sourceSets.main, 'hypixelstatsoverlay.mixins.refmap.json'
}

//noinspection GroovyMissingReturnStatement
reobf {
    jar {
        extraLines "PK: reactor com/github/grizzlt/hypixelpublicapi/shadowedLibs/reactor"
        extraLines "PK: net/hypixel/api com/github/grizzlt/hypixelpublicapi/shadowedLibs/net/hypixel/api"
        extraLines "PK: org/reactivestreams com/github/grizzlt/hypixelpublicapi/shadowedLibs/org/reactivestreams"
        extraLines "PK: io/netty/channel io/netty/channel"

        extraLines "PK: com/github/grizzlt/serverbasedmodlibrary com/github/grizzlt/hypixelstatsoverlay/shadowedLibs/com/github/grizzlt/serverbasedmodlibrary"
    }
}

jar {
    dependsOn configurations.compile
    from {
        configurations.shade.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/INDEX.LIST', 'META-INF/**'

    manifest.attributes(
            'MixinConfigs': 'hypixelstatsoverlay.mixins.json',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,

            "ForceLoadAsMod": "true",
            'FMLCorePluginContainsFMLMod': 'true',
            'FMLCorePlugin': 'com.github.grizzlt.hypixelstatsoverlay.coremod.MixinLoader'
    )
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


